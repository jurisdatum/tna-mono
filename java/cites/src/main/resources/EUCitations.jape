Phase: pass1
Input: Token
Options: control = appelt

Rule: EUCitation
Priority: 1
(
	( { Token.string == "Commission" } | { Token.string == "Council" } ) ?
	( { Token.string == "Implementing" } | { Token.string == "Delegated" } ) ?
	( { Token.string ==~ "Regulations?" } | { Token.string ==~ "Decisions?" } | { Token.string ==~ "Directives?" } ) :type
	( { Token.string == "(" }
	  ( { Token.string == "EU" } | { Token.string == "EC" } | { Token.string == "EEC" } )
	  { Token.string == ")" } ) ?
	( { Token.string == "No" }
	  ( { Token.string == "." } ) ? ) ?
	( { Token.kind == "number" } ) :number
	{ Token.string == "/" }
	( { Token.kind == "number" } ) :year
	( { Token.string == "/" }
	  ( { Token.string == "EU" } | { Token.string == "EC" } | { Token.string == "EEC" } ) ) ?
): cite --> :cite.Citation = {
	Class = :type.Token.string,
	Year = :year.Token.string,
	Number = :number.Token.string
}

Rule: SuccessiveCitation
Priority: 1
(
	( { Token.string == "," } | { Token.string == "and" } | { Token.string == "or" } )
	(
	  ( { Token.string == "(" }
	    ( { Token.string == "EU" } | { Token.string == "EC" } | { Token.string == "EEC" } )
	    { Token.string == ")" } ) ?
	  ( { Token.string == "No" }
	    ( { Token.string == "." } ) ? ) ?
	  ( { Token.kind == "number" } ) :number
	  { Token.string == "/" }
	  ( { Token.kind == "number" } ) :year
	  ( { Token.string == "/" }
	    ( { Token.string == "EU" } | { Token.string == "EC" } | { Token.string == "EEC" } ) ) ?
	): cite
) --> :cite.SuccessiveCitation = {
	Year = :year.Token.string,
	Number = :number.Token.string
}

Rule: OJCitation
Priority: 1
(
	{ Token.string == "O" }
	{ Token.string == "." }
	{ Token.string == "J" }
	{ Token.string == "." }
	{ Token.string == "No" }
	{ Token.string == "." }
	( { Token.string == "L" } | { Token.string == "C" } ) :series
	( { Token.kind == "number" } ) :issue
	{ Token.string == "," }
	( { Token.kind == "number" } ) :day
	{ Token.string == "." }
	( { Token.kind == "number" } ) :month
	{ Token.string == "." }
	( { Token.kind == "number" } ) :year
	{ Token.string == "," }
	{ Token.string == "p" }
	{ Token.string == "." }
	( { Token.kind == "number" } ) :page
): cite --> :cite.Citation = {
	Class = "OfficialJournal",
	Series = :series.Token.string,
	Issue = :issue.Token.string,
	Year = :year.Token.string,
	Month = :month.Token.string,
	Day = :day.Token.string,
	Page = :page.Token.string
}
