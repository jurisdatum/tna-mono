Phase: UKCitations2
Input: SuccessiveCitation
Options: control = appelt

Rule: UKSI
( { SuccessiveCitation.Class == "Unknown" } ) :cite --> {

	gate.AnnotationSet cite = (gate.AnnotationSet) bindings.get("cite");
	gate.Annotation ann = (gate.Annotation) cite.iterator().next();

	java.util.function.Function<gate.AnnotationSet, gate.Annotation> getPrecedingCite =
		(java.util.function.Function<gate.AnnotationSet, gate.Annotation>) this.ctx.getController().getFeatures().get("getPrecedingCite");
	gate.Annotation fullCite = getPrecedingCite.apply(cite);
	if (fullCite == null) {
		inputAS.removeAll(cite);
		return;
	}

	gate.FeatureMap features = Factory.newFeatureMap();
	features.put("Class", fullCite.getFeatures().get("Class"));
	features.put("Year", ann.getFeatures().get("Year"));
	features.put("Number", ann.getFeatures().get("Number"));
	features.put("AlternativeNumber", ann.getFeatures().get("AlternativeNumber"));
	outputAS.add(cite.firstNode(), cite.lastNode(), "Citation", features);

	inputAS.removeAll(cite);  // unnecessary?
}

Rule: WSI
( ( { SuccessiveCitation.Class == "WelshStatutoryInstrument" } ): cite ) --> :cite.Citation = {
	Class = "WelshStatutoryInstrument",
	Year = :cite.SuccessiveCitation.Year,
	Number = :cite.SuccessiveCitation.Number,
	AlternativeNumber = :cite.SuccessiveCitation.AlternativeNumber
}
