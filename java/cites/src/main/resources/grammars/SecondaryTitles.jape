Phase: SecondaryTitles
Input: Token Lookup
Options: control = appelt

Macro: PARENTHETICAL
( { Token.string == "(" }
  ( { Token.kind == "word" } | { Token.string == "-" } ) +
  { Token.string == ")" }
)

Rule: Regulation
Priority: 1
( ( ( { Token.string ==~ "[Rr]egulation" } | { Token.string ==~ "[Aa]rticle" } ) :type
    ( { Token.kind == "number" } ( { Token.string ==~ "[A-Z]{1,2}" } ) ? ) :number
    ( { Token.string == "(" } { Token.kind == "number" } { Token.string == ")" } ) ? :sub ) :link
  ( PARENTHETICAL ) ?
  { Token.string == "of" }
  ( { Token.string == "the" } ) ?
  ( { Lookup.majorType == "title" } ) :title
) --> {

	gate.AnnotationSet link = (gate.AnnotationSet) bindings.get("link");

	String sectionType = (String) bindings.get("type").iterator().next().getFeatures().get("string");
	String sectionNum = gate.Utils.stringFor(doc, bindings.get("number"));
	String sectionRef = sectionType.toLowerCase() + "-" + sectionNum.replace("(", "-").replace(")", "");;

	gate.Annotation title = (gate.Annotation) bindings.get("title").iterator().next();
	String docShortType = (String) title.getFeatures().get("minorType");
	String docType;
	if ("uksi".equals(docShortType))
		docType = "UnitedKingdomStatutoryInstrument";
	else if ("ssi".equals(docShortType))
		docType = "ScottishStatutoryInstrument";
	else
		return;
	String docYear = (String) title.getFeatures().get("year");
	String docNum = (String) title.getFeatures().get("number");
	String docId = (String) title.getFeatures().get("id");

	java.util.function.BiFunction<String, String, String> expandId =
		(java.util.function.BiFunction<String, String, String>) this.ctx.getController().getFeatures().get("expandId");

	gate.FeatureMap features = Factory.newFeatureMap();
	features.put("Class", docType);
	features.put("Year", docYear);
	features.put("Number", docNum);
	features.put("SectionRef", sectionRef);
	features.put("Title", gate.Utils.stringFor(doc, title));
	features.put("URI", expandId.apply(docId, sectionRef));
	outputAS.add(link.firstNode(), link.lastNode(), "Citation", features);

}
